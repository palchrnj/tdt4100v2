test inheritance.BSU

instance BSU bsu = new BSU(0.05, 25000.0)

sequence deposit {
	
	--
	bsu.deposit(10000.0)
	-->
	state {
		bsu.getBalance() == 10000.0
	}
	--
	bsu.deposit(-10000.0)
	throws IllegalArgumentException
	-->
	state {
		bsu.getBalance() == 10000.0
	}
	--
	bsu.deposit(20000.0)
	throws IllegalStateException
	-->
	state {
		bsu.getBalance() == 10000.0
	}
	--
	bsu.endYearUpdate(), bsu.deposit(20000.0)
	-->
	state {
		bsu.getBalance() == 10000.0 * (1 + 0.05) + 20000.0
	}
}

sequence withdraw {
	--
	bsu.deposit(20000.0), bsu.withdraw(5000.0)
	-->
	state {
		bsu.getBalance() == 15000.0
	}
	--
	bsu.withdraw(-10000.0) throws IllegalArgumentException
	-->
	state {
		bsu.getBalance() == 15000.0
	}
	--
	bsu.withdraw(20000.0) throws IllegalStateException
	-->
	state {
		bsu.getBalance() == 15000.0
	}
	--
	bsu.endYearUpdate(), bsu.withdraw(10000.0) throws IllegalStateException
	-->
	state {
		bsu.getBalance() == 15000.0 * (1 + 0.05)
	}
}

sequence getTaxDeduction {
	--
	bsu.deposit(20000.0)
	-->
	state {
		bsu.getTaxDeduction() == 20000.0 * 0.20
	}
	--
	bsu.endYearUpdate, bsu.deposit(10000.0)
	-->
	state {
		bsu.getTaxDeduction() == 10000.0 * 0.20
	}
	--
	bsu.endYearUpdate
	-->
	state {
		bsu.getTaxDeduction() == 0.0
	}
}

method boolean ==(double n1, double n2) {
	val epsilon = 0.0000001 // avrundingsfeil
	n1 - epsilon <= n2 && n1 + epsilon >= n2
}